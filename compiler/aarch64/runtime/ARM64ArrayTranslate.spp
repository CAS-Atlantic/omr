/*******************************************************************************
 * Copyright IBM Corp. and others 2024
 *
 * This program and the accompanying materials are made available under
 * the terms of the Eclipse Public License 2.0 which accompanies this
 * distribution and is available at https://www.eclipse.org/legal/epl-2.0/
 * or the Apache License, Version 2.0 which accompanies this distribution
 * and is available at https://www.apache.org/licenses/LICENSE-2.0.
 *
 * This Source Code may also be made available under the following Secondary
 * Licenses when the conditions for such availability set forth in the
 * Eclipse Public License, v. 2.0 are satisfied: GNU General Public License,
 * version 2 with the GNU Classpath Exception [1] and GNU General Public
 * License, version 2 with the OpenJDK Assembly Exception [2].
 *
 * [1] https://www.gnu.org/software/classpath/license.html
 * [2] https://openjdk.org/legal/assembly-exception.html
 *
 * SPDX-License-Identifier: EPL-2.0 OR Apache-2.0 OR GPL-2.0-only WITH Classpath-exception-2.0 OR GPL-2.0-only WITH OpenJDK-assembly-exception-1.0
 *******************************************************************************/

	.file "ARM64ArrayTranslate.s"

#include "aarch64/runtime/arm64asmdefs.inc"

	.globl	FUNC_LABEL(__arrayTranslateTRTO)
	.globl	FUNC_LABEL(__arrayTranslateTRTO255)
	.globl	FUNC_LABEL(__arrayTranslateTROTNoBreak)

	.text
	.align	2

// ----
// arrayTranslateTRTO
// ----
// TO stands for Two bytes to One byte
//
// uint16 input[];
// uint8  output[];
// int32  len;
// uint16 mask; // 0xff00ff00 (ISO8859) or 0xff80ff80 (ASCII)
//
// int32 i = 0;
// while (i < len) {
//     uint16 ch = input[i];
//     if ((ch & mask) != 0) break;
//     output[i] = ch & 0xFF;
//     i++;
// }
// return i;
//
// in:  x0: input
//      x1: output
//      x2: len
//      x3: mask
// out: x0: num of translated elements
// trash: x4-x6, v0-v3

FUNC_LABEL(__arrayTranslateTRTO):
	// preserve output address
	mov	x6, x1
	// load mask to a SIMD register
	dup	v3.8h, w3
	lsr	w4, w2, #4
	cbz	w4, atTRTO_15
atTRTO_16Loop:
	// load 16 elements
	ldp	q0, q1, [x0]
	// mask first 8 elements
	and	v2.16b, v0.16b, v3.16b
	// fail when any one of them is non-zero
	umaxp	v2.4s, v2.4s, v2.4s
	mov	x5, v2.D[0]
	cbnz	x5, atTRTO_Fail8
	// mask next 8 elements
	and	v2.16b, v1.16b, v3.16b
	// fail when any one of them is non-zero
	umaxp	v2.4s, v2.4s, v2.4s
	mov	x5, v2.D[0]
	cbnz	x5, atTRTO_Fail16
	// collect lower 8 bits
	uzp1	v2.16b, v0.16b, v1.16b
	add	x0, x0, #32
	subs	w4, w4, #1
	// store 16 elements
	str	q2, [x1], #16
	b.ne	atTRTO_16Loop
atTRTO_15:
	// 15 elements or less remaining
	tbz	w2, #3, atTRTO_7		// test bit 3
	// load 8 elements
	ldr	q0, [x0]
	// mask 8 elements
	and	v2.16b, v0.16b, v3.16b
	// fail when any one of them is non-zero
	umaxp	v2.4s, v2.4s, v2.4s
	mov	x5, v2.D[0]
	cbnz	x5, atTRTO_Fail8
	// collect lower 8 bits
	xtn	v2.8b, v0.8h
	add	x0, x0, #16
	// store 8 elements
	str	d2, [x1], #8
atTRTO_7:
	// 7 elements or less remaining
	tbz	w2, #2, atTRTO_3		// test bit 2
	// load 4 elements
	ldr	d0, [x0]
	// mask 4 elements
	and	v2.8b, v0.8b, v3.8b
	// fail when any one of them is non-zero
	mov	x5, v2.D[0]
	cbnz	x5, atTRTO_Fail8
	// collect lower 8 bits
	xtn	v2.8b, v0.8h
	add	x0, x0, #8
	// store 4 elements
	str	s2, [x1], #4
atTRTO_3:
	// 3 elements or less remaining
	ands	w4, w2, #3
atTRTO_1Loop:
	b.eq	atTRTO_Done
	ldrh	w5, [x0], #2
	tst	w5, w3
	b.ne	atTRTO_Done
	subs	w4, w4, #1
	strb	w5, [x1], #1
	b	atTRTO_1Loop
atTRTO_Fail16:
	// 16 elements in v0 and v1 -- No need to check v0
	// collect lower 8 bits
	xtn	v2.8b, v0.8h
	// store 8 elements
	str	d2, [x1], #8
	mov	v0.16b, v1.16b
	// fall through
atTRTO_Fail8:
	// 8 or 4 elements in v0
	umov	w5, v0.H[0]
	tst	w5, w3
	b.ne	atTRTO_Done
	strb	w5, [x1], #1
	// shift v0 to right 16 bits
	ext	v0.16b, v0.16b, v0.16b, #2
	b	atTRTO_Fail8
atTRTO_Done:
	// number of translated elements
	sub	x0, x1, x6
	ret

// ----
// arrayTranslateTRTO255
// ----
// TO stands for Two bytes to One byte
//
// uint16 input[];
// uint8  output[];
// int32  len;
//
// int32 i = 0;
// for (i < len) {
//     uint16 ch = input[i];
//     if (ch > 0xFF) break;
//     output[i] = ch & 0xFF;
//     i++;
// }
// return i;
//
// in:  x0: input
//      x1: output
//      x2: len
// out: x0: num of translated elements
// trash: x4-x6, v0-v2

FUNC_LABEL(__arrayTranslateTRTO255):
	// preserve output address
	mov	x6, x1
	lsr	w4, w2, #4
	cbz	w4, atTRTO255_15
atTRTO255_16Loop:
	// load 16 elements
	ldp	q0, q1, [x0]
	// collect upper 8 bits
	uzp2	v2.16b, v0.16b, v1.16b
	// fail when any one of them is non-zero
	umaxp	v2.4s, v2.4s, v2.4s
	mov	x5, v2.D[0]
	cbnz	x5, atTRTO255_Fail16
	// collect lower 8 bits
	uzp1	v2.16b, v0.16b, v1.16b
	add	x0, x0, #32
	subs	w4, w4, #1
	// store 16 elements
	str	q2, [x1], #16
	b.ne	atTRTO255_16Loop
atTRTO255_15:
	// 15 elements or less remaining
	tbz	w2, #3, atTRTO255_7		// test bit 3
	// load 8 elements
	ldr	q0, [x0]
	// collect upper 8 bits
	shrn	v2.8b, v0.8h, #8
	// fail when any one of them is non-zero
	mov	x5, v2.D[0]
	cbnz	x5, atTRTO255_Fail8
	// collect lower 8 bits
	xtn	v2.8b, v0.8h
	add	x0, x0, #16
	// store 8 elements
	str	d2, [x1], #8
atTRTO255_7:
	// 7 elements or less remaining
	tbz	w2, #2, atTRTO255_3		// test bit 2
	// load 4 elements
	ldr	d0, [x0]
	// collect upper 8 bits
	shrn	v2.8b, v0.8h, #8
	// fail when any one of them is non-zero
	mov	w5, v2.S[0]
	cbnz	w5, atTRTO255_Fail8
	// collect lower 8 bits
	xtn	v2.8b, v0.8h
	add	x0, x0, #8
	// store 4 elements
	str	s2, [x1], #4
atTRTO255_3:
	// 3 elements or less remaining
	ands	w4, w2, #3
atTRTO255_1Loop:
	b.eq	atTRTO255_Done
	ldrh	w5, [x0], #2
	cmp	w5, #256
	b.cs	atTRTO255_Done
	subs	w4, w4, #1
	strb	w5, [x1], #1
	b	atTRTO255_1Loop
atTRTO255_Fail16:
	// 16 elements in v0 and v1
	// collect upper 8 bits
	shrn	v2.8b, v0.8h, #8
	mov	x5, v2.D[0]
	cbnz	x5, atTRTO255_Fail8
	// collect lower 8 bits
	xtn	v2.8b, v0.8h
	// store 8 elements
	str	d2, [x1], #8
	mov	v0.16b, v1.16b
	// fall through
atTRTO255_Fail8:
	// 8 or 4 elements in v0
	umov	w5, v0.H[0]
	cmp	w5, #256
	b.cs	atTRTO255_Done
	strb	w5, [x1], #1
	// shift v0 to right 16 bits
	ext	v0.16b, v0.16b, v0.16b, #2
	b	atTRTO255_Fail8
atTRTO255_Done:
	// number of translated elements
	sub	x0, x1, x6
	ret

// ----
// arrayTranslateTROTNoBreak
// ----
// OT stands for One byte to Two bytes
//
// uint8  input[];
// uint16 output[];
// int32  len;
//
// int32 i = 0;
// while (i < len) {
//     output[i] = (uint16)input[i];
//     i++;
// }
// return i;
//
// in:  x0: input
//      x1: output
//      x2: len
// out: x0: num of translated elements
// trash: x4-x5, v0-v1

FUNC_LABEL(__arrayTranslateTROTNoBreak):
	cmp	w2, #16
	b.cc	atTROTNB_residue
	lsr	w4, w2, #4
atTROTNB_16Loop:
	// load 16 elements
	ldr	q0, [x0], #16
	// unsigned extension
	uxtl	v1.8h, v0.8b
	uxtl2	v0.8h, v0.16b
	subs	w4, w4, #1
	// store 16 elements
	stp	q1, q0, [x1], #32
	b.ne	atTROTNB_16Loop
atTROTNB_residue:
	// 15 elements or less remaining
	adr	x5, atTROTNB_table
	and	w4, w2, #15
	add	x5, x5, x4, lsl #2
	br	x5

atTROTNB_table:
	b	atTROTNB_0
	b	atTROTNB_1
	b	atTROTNB_2
	b	atTROTNB_3
	b	atTROTNB_4
	b	atTROTNB_5
	b	atTROTNB_6
	b	atTROTNB_7
	b	atTROTNB_8
	b	atTROTNB_9
	b	atTROTNB_10
	b	atTROTNB_11
	b	atTROTNB_12
	b	atTROTNB_13
	b	atTROTNB_14

atTROTNB_15:
	// load and store 8 elements
	ldr	d0, [x0], #8
	uxtl	v1.8h, v0.8b
	str	q1, [x1], #16
atTROTNB_7:
	// load and store 4 elements
	ldr	s0, [x0]
	uxtl	v1.8h, v0.8b
	str	d1, [x1]
	// load and store 2 elements
	ldr	h0, [x0, #4]
	uxtl	v1.8h, v0.8b
	str	s1, [x1, #8]
	// load and store 1 element
	ldrb	w4, [x0, #6]
	strh	w4, [x1, #12]
	mov	x0, x2
	ret

atTROTNB_14:
	// load and store 8 elements
	ldr	d0, [x0], #8
	uxtl	v1.8h, v0.8b
	str	q1, [x1], #16
atTROTNB_6:
	// load and store 4 elements
	ldr	s0, [x0]
	uxtl	v1.8h, v0.8b
	str	d1, [x1]
	// load and store 2 elements
	ldr	h0, [x0, #4]
	uxtl	v1.8h, v0.8b
	str	s1, [x1, #8]
	mov	x0, x2
	ret

atTROTNB_13:
	// load and store 8 elements
	ldr	d0, [x0], #8
	uxtl	v1.8h, v0.8b
	str	q1, [x1], #16
atTROTNB_5:
	// load and store 4 elements
	ldr	s0, [x0]
	uxtl	v1.8h, v0.8b
	str	d1, [x1]
	// load and store 1 element
	ldrb	w4, [x0, #4]
	strh	w4, [x1, #8]
	mov	x0, x2
	ret

atTROTNB_12:
	// load and store 8 elements
	ldr	d0, [x0], #8
	uxtl	v1.8h, v0.8b
	str	q1, [x1], #16
atTROTNB_4:
	// load and store 4 elements
	ldr	s0, [x0]
	uxtl	v1.8h, v0.8b
	str	d1, [x1]
	mov	x0, x2
	ret

atTROTNB_11:
	// load and store 8 elements
	ldr	d0, [x0], #8
	uxtl	v1.8h, v0.8b
	str	q1, [x1], #16
atTROTNB_3:
	// load and store 2 elements
	ldr	h0, [x0]
	uxtl	v1.8h, v0.8b
	str	s1, [x1]
	// load and store 1 element
	ldrb	w4, [x0, #2]
	strh	w4, [x1, #4]
	mov	x0, x2
	ret

atTROTNB_10:
	// load and store 8 elements
	ldr	d0, [x0], #8
	uxtl	v1.8h, v0.8b
	str	q1, [x1], #16
atTROTNB_2:
	// load and store 2 elements
	ldr	h0, [x0]
	uxtl	v1.8h, v0.8b
	str	s1, [x1]
	mov	x0, x2
	ret

atTROTNB_9:
	// load and store 8 elements
	ldr	d0, [x0], #8
	uxtl	v1.8h, v0.8b
	str	q1, [x1], #16
atTROTNB_1:
	// load and store 1 element
	ldrb	w4, [x0]
	strh	w4, [x1]
	mov	x0, x2
	ret

atTROTNB_8:
	// load and store 8 elements
	ldr	d0, [x0]
	uxtl	v1.8h, v0.8b
	str	q1, [x1]
atTROTNB_0:
	mov	x0, x2
	ret
